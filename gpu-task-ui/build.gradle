plugins {
    id 'org.beryx.jlink' version '2.25.0'
    id "de.jjohannes.extra-java-module-info" version "0.12"
}

def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

application {
    mainModule = 'hp.community.gpu.app.ui'
    mainClass = 'hp.community.gpu.app.ui.GpuLoaderApplication'
}

group 'hp.community.gpu.app.ui'

dependencies {

    // Service
    implementation project(':gpu-task-service')

    // Implementation
    implementation project(':gpu-task-core')

    // JavaFX
    implementation "org.openjfx:javafx-base:18.0.1:${platform}"
    implementation "org.openjfx:javafx-controls:18.0.1:${platform}"
    implementation "org.openjfx:javafx-graphics:18.0.1:${platform}"
    implementation "org.openjfx:javafx-fxml:18.0.1:${platform}"

    // Slf4j
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha7'

    // APARAPI
    implementation 'com.aparapi:aparapi:3.0.0'
}

extraJavaModuleInfo {
    automaticModule("aparapi-3.0.0.jar", "aparapi")
    automaticModule("aparapi-jni-1.4.3.jar", "aparapi.jni")
}

startScripts {
    defaultJvmOpts = ['-Dtask=Default']
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'amd-gpu'
        noConsole = false
    }
}
